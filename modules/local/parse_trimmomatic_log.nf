process PARSE_TRIMMOMATIC_LOG {
    tag "$meta.id"
    label 'process_medium'

    conda (params.enable_conda ? "conda-forge::python=3.8.3" : null)
    container "${ workflow.containerEngine == 'singularity' && !task.ext.singularity_pull_docker_container ?
        'https://depot.galaxyproject.org/singularity/python:3.8.3' :
        'quay.io/biocontainers/python:3.8.3' }"

    input:
    tuple val(meta), path("trimlog.txt")
    val min_reads

    output:
    tuple val(meta), path("*_report.txt"), emit: report
    tuple val(meta), path("*.csv")       , emit: csv
    tuple val(meta), path("*.log")       , emit: log
    path "versions.yml"                  , emit: versions

    when:
    task.ext.when == null || task.ext.when

    script: // This script is bundled with the pipeline, in nf-core/legiocluster/bin/
    def args = task.ext.args ?: ''
    def prefix = task.ext.prefix ?: "${meta.id}"
    """
    parse_trimmomatic_log.py \\
        --output-file ${prefix}.csv \\
        --report-file ${prefix}_report.txt \\
        --trimmomatic-log-file trimlog.txt \\
        --min-reads $min_reads \\
        $args \\
        > ${prefix}.log

    cat <<-END_VERSIONS > versions.yml
    "${task.process}":
        python: \$(python --version | sed 's/Python //g')
    END_VERSIONS
    """
}
